# Copyright 2014 The Goma Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

config("debug") {
  defines = [ "_DEBUG" ]
  if (os == "linux") {
    defines += [ "_GLIBCXX_DEBUG=1" ]
  }
}

config("release") {
  defines = [ "NDEBUG" ]
}

config("default_libs") {
  if (os == "win") {
    libs = [
      "advapi32.lib",
      "delayimp.lib",
      "psapi.lib",
      "shell32.lib",
    ]

    # These are to avoid loading gdi32.dll.
    # See b/115990434
    ldflags = [
      # This is for absl/base/internal/sysinfo.cc .
      "/DELAYLOAD:shlwapi.dll",

      # This is for GetUserNameA.
      "/DELAYLOAD:advapi32.dll",
    ]
  }
}

# Dependencies that all executables and shared libraries should have.
group("exe_and_shlib_deps") {
  public_deps = []
  if (using_sanitizer) {
    public_deps += [ "//build/config/sanitizers:deps" ]
  }
  if (use_custom_libcxx) {
    public_deps += [ "//third_party/libc++" ]
  }

  #if (use_afl) {
  #  public_deps += [ "//third_party/afl" ]
  #}
}

config("feature_flags") {
  defines = []
  if (is_asan) {
    defines += [ "ADDRESS_SANITIZER" ]
  }
  if (is_tsan) {
    defines += [ "THREAD_SANITIZER" ]
  }
  if (is_msan) {
    defines += [ "MEMORY_SANITIZER" ]
  }
}
