# Copyright 2014 The Goma Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

roots = "roots"

if (os == "linux") {
  genc = "//client/genc.py"

  action("gen_roots_certs") {
    script = genc
    sources = [
      "$roots.pem",
    ]
    outputs = [
      "$target_gen_dir/$roots.c",
      "$target_gen_dir/$roots.h",
    ]
    args = [
      "--prefix=certs_",
      "--out-dir",
      rebase_path("$target_gen_dir"),
      rebase_path("$roots.pem"),
    ]
  }

  action("gen_dst_certs") {
    script = genc
    sources = [
      "DST_Root_CA_X3.pem",
    ]
    outputs = [
      "$target_gen_dir/DST_Root_CA_X3.c",
      "$target_gen_dir/DST_Root_CA_X3.h",
    ]
    args = [
      "--prefix=certs_",
      "--out-dir",
      rebase_path("$target_gen_dir"),
      rebase_path("DST_Root_CA_X3.pem"),
    ]
  }

  config("certs_config") {
    include_dirs = [ "$target_gen_dir" ]
  }
  static_library("certs") {
    sources = [
      "$target_gen_dir/$roots.c",
      "$target_gen_dir/$roots.h",
      "$target_gen_dir/DST_Root_CA_X3.c",
      "$target_gen_dir/DST_Root_CA_X3.h",
    ]
    deps = [
      ":gen_dst_certs",
      ":gen_roots_certs",
    ]
    public_configs = [ ":certs_config" ]
  }
}

if (os == "win") {
  config("certs_resource_config") {
    include_dirs = [ "." ]
  }

  # This needs to be source_set (instead of static_library), otherwise
  # openssl_engine will fail to read certs.rc.
  source_set("certs_resource") {
    sources = [
      "$roots.pem",
      "DST_Root_CA_X3.pem",
      "certs.rc",
      "certs_resource.h",
    ]
    public_configs = [ ":certs_resource_config" ]
  }
}
